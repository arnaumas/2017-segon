set terminal epslatex size 10cm,6cm color colortext header '\sffamily \footnotesize'
set output 'secular.tex'

#Activitat de l'estany
f(x) =  (2.4E-4/0.0036) * exp(2.4E-4*x)
#Activitat de l'indi
g(x) = (2.4E-4*0.4)/(0.0036*(0.4 - 2.4E-4)) * (exp(-2.4E-4*x) - exp(-0.4*x))

set xrange [0:20]
set xtics scale 0.75 0,2,20
set ytics scale 0.75

set xlabel 'Temps (h)'
set ylabel 'Activitat per nucli (\si{\micro Bq})'
set format '$\mathsf{%g}$'

set border linewidth 1.5
set style line 1 linecolor rgb "blue" dashtype 1 linewidth 2
set style line 2 linecolor rgb "violet" dashtype 1 linewidth 2

set label '$\mathsf{ {}^{113}\text{Sn}}$' at 1,0.06 
set label '$\mathsf{ {}^{113}\text{In}^{\ast}}$' at 5,0.05 

plot f(x) notitle with lines linestyle 1 , g(x) notitle with lines linestyle 2
